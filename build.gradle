import org.ajoberstar.grgit.Grgit

apply plugin: 'java'

sourceCompatibility = '1.8'

defaultTasks 'clean', 'build'

jar {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.Main'
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

def buildNumber = ""
ext {
    //noinspection GrDeprecatedAPIUsage  不会用新版
    git = Grgit.open(file(".git"))
    parents = git.head().parentIds
    index = 0// Offset to match CI
    for (; parents != null && !parents.isEmpty(); index++) {
        parents = git.getResolve().toCommit(parents.get(0)).getParentIds()
    }
    //noinspection GrReassignedInClosureLocalVar
    buildNumber = "${index}"
}
version = "$version-b$buildNumber"

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = "nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    maven { url = 'https://raw.github.com/FabioZumbi12/UltimateChat/mvn-repo/' }
    maven { url = 'https://jitpack.io' }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.0.0'
    compile 'br.net.fabiozumbi12.UltimateChat:UltimateChat-Sponge-5.0-6.0:1.8.6'
    compile 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
    compile "io.github.nucleuspowered:nucleus-api:1.3.9-S7.0"
}
