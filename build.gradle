import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

plugins {
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

defaultTasks 'clean', 'build'

jar {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.common.Main'
    }
}

def buildNumber = ""
ext {
    //代码来自插件ebi中的build.gradle → : https://github.com/euOnmyoji/EpicBanItem---Sponge
    //noinspection GrDeprecatedAPIUsage  不会用新版
    git = Grgit.open(file(".git"))
    parents = git.head().parentIds
    index = 0// Offset to match CI
    for (; parents != null && !parents.isEmpty(); index++) {
        parents = git.getResolve().toCommit(parents.get(0)).getParentIds()
    }
    //noinspection GrReassignedInClosureLocalVar
    buildNumber = "${index}"
}

blossom {
    //参考build.gradle https://github.com/ginyai/GiftCode/blob/master/build.gradle
    replaceToken('@version@', version)
}

version = "$version-b$buildNumber"

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = "nucleus"
        url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
    }
    maven { url = 'https://raw.github.com/FabioZumbi12/UltimateChat/mvn-repo/' }
    maven { url = 'https://jitpack.io' }

    //bukkit
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeapi}"
    compileOnly 'br.net.fabiozumbi12.UltimateChat:UltimateChat-Sponge-5.0-6.0:1.8.6'
    compileOnly 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
    compileOnly 'io.github.nucleuspowered:nucleus-api:1.3.9-S7.0'
    compile 'org.bstats.bStats-Metrics:bstats-sponge:1.3'

    //bukkit
    compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
}

sourceSets {
    api {
        resources.srcDir 'com/github/euonmyoji/newhonor/api'
    }
}

//以下有关shadow代码由百度搜索引擎提供技术支持(雾)
shadowJar() {
    classifier = ""
    dependencies {
        //noinspection GroovyAssignabilityCheck # ??? qwq
        include(dependency("org.bstats.bStats-Metrics:bstats-sponge:1.3"))
    }
}

task spongeJar(type: ShadowJar) {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.common.Main'
    }
    configurations = [project.configurations.compile]
    classifier = "SpongeAPI${spongeapi}"
    from sourceSets.main.output
    exclude 'com/github/euonmyoji/newhonor/bukkit/**'

    dependencies {
        //noinspection GroovyAssignabilityCheck # ??? qwq
        include(dependency("org.bstats.bStats-Metrics:bstats-sponge:1.3"))
    }
}

task bukkitJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.github.euonmyoji.newhonor.common.Main'
    }
    classifier = "Bukkit"
    from sourceSets.main.output
    exclude 'com/github/euonmyoji/newhonor/sponge/**'
}

artifacts {
    archives shadowJar
    archives spongeJar
    archives bukkitJar
}